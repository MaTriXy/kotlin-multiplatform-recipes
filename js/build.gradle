import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'kotlin-platform-js'

archivesBaseName = 'kotlin-multiplatform-recipes-js'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implement project(':common')
    compile libraries.kotlin_stdlib_js
    testCompile libraries.kotlin_test_js
}

task copyTestCompileLibrariesToQunitReportDir {
    def outputDirectoryPath = "${buildDir}/reports/tests/qunit/lib"
    inputs.files configurations.testCompile
    outputs.files file(outputDirectoryPath)

    doLast {
        configurations.testCompile.each { File file ->
            copy {
                includeEmptyDirs = false

                from zipTree(file.absolutePath)
                into outputDirectoryPath
                include { fileTreeElement ->
                    def path = fileTreeElement.path
                    path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
                }
            }
        }
    }

}

task copyTestHtmlToQunitReportDir(type: Copy) {
    from file('src/test/html')
    inputs.property("qunitVersion", qunitVersion)
    into "${buildDir}/reports/tests/qunit/html"
    filter(ReplaceTokens, tokens: [qunitVersion: qunitVersion])
}

test.dependsOn copyTestCompileLibrariesToQunitReportDir, copyTestHtmlToQunitReportDir

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.kotlin
}

artifacts {
    archives sourcesJar
}
