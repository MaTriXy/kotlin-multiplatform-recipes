apply plugin: 'kotlin2js'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile project(':js')
    compile libraries.kotlin_stdlib_js
}

[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions.moduleKind = "umd"
}

task copyCompileLibrariesToBuildDir {
    inputs.files configurations.compile
    outputs.files file(temporaryDir)

    doLast {
        configurations.compile.each { File file ->
            copy {
                includeEmptyDirs = false

                from zipTree(file.absolutePath)
                into temporaryDir
                include { fileTreeElement ->
                    def path = fileTreeElement.path
                    path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
                }
            }
        }
    }
}

compileKotlin2Js.dependsOn copyCompileLibrariesToBuildDir
